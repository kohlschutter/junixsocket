plugins {
    id 'com.android.application'
}

android {
    namespace 'com.kohlschutter.junixsocket.selftest.android'
    compileSdk 33
    ndkVersion '25.2.9519653'

    defaultConfig {
        applicationId "com.kohlschutter.junixsocket.selftest.android"
        minSdk 26
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }

    packagingOptions {
        // junit jars contain duplicate META-INF/LICENSE.md files
        resources.excludes += "META-INF/LICENSE*"

        // exclude GraalVM config (not necessary on Android)
        resources.excludes += "META-INF/native-image/**/*"

        // exclude other native image files erroneously included
        resources.excludes += "lib/*/jni/*"
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation 'com.kohlschutter.junixsocket:junixsocket-native-android:2.10.1@aar'

    implementation 'com.kohlschutter.junixsocket:junixsocket-common:2.10.1'

    implementation 'com.kohlschutter.junixsocket:junixsocket-tipc:2.10.1'
    implementation 'com.kohlschutter.junixsocket:junixsocket-vsock:2.10.1'
    implementation 'com.kohlschutter.junixsocket:junixsocket-rmi:2.10.1'
    implementation 'com.kohlschutter.junixsocket:junixsocket-ssl:2.10.1'
    implementation 'com.kohlschutter.junixsocket:junixsocket-darwin:2.10.1'

    // junixsocket-ssl will automatically use the real Bouncycastle on Android when available.
    // This is REQUIRED for junixsocket-ssl tests and RECOMMENDED for junixsocket-ssl users.
    // Android's built-in Bouncycastle fork lacks PKCS12 support.
    implementation 'org.bouncycastle:bcprov-jdk18on:1.76'

    // junixsocket-ssl will automatically use Bouncycastle-TLS on Android when available.
    // This is REQUIRED for junixsocket-ssl tests and RECOMMENDED for junixsocket-ssl users.
    // Android's built-in TLS code has problems with sending SNI hostname requests.
    implementation 'org.bouncycastle:bctls-jdk18on:1.76'

    // selftest-specifics
    implementation('com.kohlschutter.junixsocket:junixsocket-selftest:2.10.1') {
        exclude group: "com.kohlschutter.junixsocket", module: "junixsocket-core"
        exclude group: "com.kohlschutter.junixsocket", module: "junixsocket-native-common"
        exclude group: "com.kohlschutter.junixsocket", module: "junixsocket-native-custom"
    }
    implementation 'com.kohlschutter:kohlschutter-util:1.6.3'
    implementation 'com.kohlschutter:kohlschutter-test-util:1.6.3'
    implementation 'org.junit.platform:junit-platform-console:1.9.3'
    implementation 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
    implementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'

    // testImplementation 'junit:junit:4.13.2'
    //androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    //androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
}
